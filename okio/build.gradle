apply plugin: 'org.jetbrains.kotlin.multiplatform'

kotlin {
  targets {
    // egor: jvmWithJava is getting deprecated but it's not clear what the replacement is. For now 
    // gonna keep it inside targets {}
    fromPreset(presets.jvmWithJava, 'jvm') {
      project.sourceSets.main.java.setSrcDirs(['jvm/src/main/java'])
      project.sourceSets.test.java.setSrcDirs(['jvm/src/test/java'])
      project.sourceSets.main.resources.setSrcDirs(['jvm/src/main/resources'])
      project.sourceSets.main.kotlin.setSrcDirs(['jvm/src/main/kotlin'])
      project.sourceSets.test.kotlin.setSrcDirs(['jvm/src/test/kotlin'])
    }
  }
  js() {
    configure([compilations.main, compilations.test]) {
      tasks.getByName(compileKotlinTaskName).kotlinOptions {
        moduleKind = "commonjs"
      }
    }
  }
  iosX64() {
    binaries {
      framework()
    }
  }
  iosArm64() {
    binaries {
      framework()
    }
  }
  // Required to generate tests tasks: https://youtrack.jetbrains.com/issue/KT-26547
  linuxX64()
  macosX64()
  mingwX64('winX64')
  sourceSets {
    commonMain {
      kotlin.setSrcDirs(['src/main/kotlin'])
      dependencies {
        api deps.kotlin.stdLib.common
      }
    }
    commonTest {
      kotlin.setSrcDirs(['src/test/kotlin'])
      dependencies {
        implementation deps.kotlin.test.common
        implementation deps.kotlin.test.annotations
      }
    }
    jvmMain {
      kotlin.setSrcDirs(['jvm/src/main/java'])
      resources.setSrcDirs(['jvm/src/main/resources'])
      dependencies {
        api deps.kotlin.stdLib.jdk6
        compileOnly deps.animalSniffer.annotations
      }
    }
    jvmTest {
      kotlin.setSrcDirs(['jvm/src/test/java'])
      dependencies {
        implementation deps.test.junit
        implementation deps.test.assertj
        implementation deps.kotlin.test.jdk
      }
    }
    jsMain {
      kotlin.setSrcDirs(['js/src/main/kotlin'])
      dependencies {
        api deps.kotlin.stdLib.js
      }
    }
    jsTest {
      kotlin.setSrcDirs(['js/src/test/kotlin'])
      dependencies {
        implementation deps.kotlin.test.js
      }
    }
    nativeMain {
      kotlin.setSrcDirs(['native/src/main/kotlin'])
      dependsOn commonMain
    }
    nativeTest {
      kotlin.setSrcDirs(['native/src/test/kotlin'])
      dependsOn commonTest
    }

    configure([iosX64Main, iosArm64Main, linuxX64Main, macosX64Main, winX64Main]) {
      dependsOn nativeMain
    }
    configure([iosX64Test, iosArm64Test, linuxX64Test, macosX64Test, winX64Test]) {
      dependsOn nativeTest
    }
  }
}

apply from: 'js/build.gradle'
apply from: 'jvm/jvm.gradle'
apply from: "$rootDir/gradle/gradle-mvn-mpp-push.gradle"
